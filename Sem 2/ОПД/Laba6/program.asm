            ORG             0x0     ; Инициализация векторов прерывания
V0:         WORD $DEFAULT,  0x180   ; Вектор прерывания #0
V1:         WORD $INT1,     0x180   ; Вектор прерывания #1
V2:         WORD $DEFAULT,  0x180   ; ...
V3:         WORD $INT3,     0x180   ;
V4:         WORD $DEFAULT,  0x180   ;
V5:         WORD $DEFAULT,  0x180   ;
V6:         WORD $DEFAULT,  0x180   ;
V7:         WORD $DEFAULT,  0x180   ; Вектор прерывания #7
                                    ;
DEFAULT:    IRET                    ; Просто возврат
                                    ;
            ORG 0x19                ;
X:          WORD 30                 ; Переменная X
MAX_X:      WORD 43                 ; Максимальное значение X + 1
MIN_X:      WORD -42                ; Минимальное значение X
                                    ;
START:      DI                      ; Загрузка начальных векторов прерывания
            CLA                     ;
            OUT 0x1                 ; Запрет прерываний для ВУ-0
            OUT 0x5                 ; Запрет прерываний для ВУ-2
            OUT 0xB                 ; Запрет прерываний для ВУ-4
            OUT 0xE                 ; Запрет прерываний для ВУ-5
            OUT 0x12                ; Запрет прерываний для ВУ-6
            OUT 0x16                ; Запрет прерываний для ВУ-7
            OUT 0x1A                ; Запрет прерываний для ВУ-8
            OUT 0x1E                ; Запрет прерываний для ВУ-9
            LD #0x9                 ;
            OUT 0x3                 ; Установка вектора прерывания 1 для ВУ-1
            LD #0xB                 ;
            OUT 0x7                 ; Установка вектора прерывания 3 для ВУ-3
            LD X                    ; Загрузка X
            CALL CHECK_X            ; Проверка X
            ST X                    ; Сохранение X
            EI                      ; Разрешить прерывания
                                    ;
                                    ; Цикл основной программы
PROG:       LD X                    ; X - 3 -> X
            SUB #0x3                ;
            CALL CHECK_X            ;
            ST X                    ;
            NOP                     ; Точка останова
            JUMP PROG               ;
                                    ;
                                    ; Обработка прерываний для ВУ-1
INT1:       DI                      ; Запрет прерываний
            NOP                     ; Точка останова
            PUSH                    ; -3 * X -> (DR ВУ-1)
            LD X                    ;
            ASL                     ;
            ADD X                   ;
            NEG                     ;
            OUT 0x2                 ;
            POP                     ;
            NOP                     ; Точка останова
            EI                      ; Разрешить прерывания
            IRET                    ; Выход из прерывания
                                    ;
                                    ; Обработка прерываний для ВУ-3
INT3:       DI                      ; Запрет прерываний
            NOP                     ; Точка останова
            PUSH                    ; X - (DR ВУ-3) -> X
            IN 0x6                  ;
            SXTB                    ;
            SUB X                   ;
            CALL CHECK_X            ;
            ST X                    ;
            POP                     ;
            NOP                     ; Точка останова
            EI                      ; Разрешить прерывания
            IRET                    ; Выход из прерывания
                                    ;
CHECK_X:    CMP MIN_X               ; Проверка X на ОДЗ
            BMI LD_MIN_X            ;
            CMP MAX_X               ;
            BPL LD_MIN_X            ;
            RET                     ;
                                    ;
LD_MIN_X:   LD MIN_X                ;
            RET                     ;